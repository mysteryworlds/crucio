allprojects {
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  group = 'com.mysteryworlds.crucio'
  version = '1.0.0-SNAPSHOT'

  sourceCompatibility = '11'
  targetCompatibility = '11'

  repositories {
    google()
    mavenLocal()
    mavenCentral()
    maven {
      url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
      content {
        includeGroup 'org.bukkit'
        includeGroup 'org.spigotmc'
      }
    }
    maven {
      url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/mysteryworlds/crucio")
        credentials {
          username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
          password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
      }
    }
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
  }
}
